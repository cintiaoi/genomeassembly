/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    Config file for defining DSL2 per module options and publishing paths
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    Available keys to override module options:
        ext.args   = Additional arguments appended to command in module.
        ext.args2  = Second set of arguments appended to command in module (multi-tool modules).
        ext.args3  = Third set of arguments appended to command in module (multi-tool modules).
        ext.prefix = File name prefix for output files.
----------------------------------------------------------------------------------------
*/

process {

    publishDir = [
        path: { "${params.outdir}/${task.process.tokenize(':')[-1].tokenize('_')[0].toLowerCase()}" },
        mode: params.publish_dir_mode,
        saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
    ]

    withName: SAMPLESHEET_CHECK {
        publishDir = [
            path: { "${params.outdir}/pipeline_info" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: FASTQC {
        ext.args = '--quiet'
    }

    withName: CUSTOM_DUMPSOFTWAREVERSIONS {
        publishDir = [
            path: { "${params.outdir}/pipeline_info" },
            mode: params.publish_dir_mode,
            pattern: '*_versions.yml'
        ]
    }

    // Set up of the polishing pipeline
    withName: LONGRANGER_ALIGN {

        ext.args = "--disable-ui --nopreflight"
        if(System.getenv('GITHUB_ACTION') != null ) {
            container = "ghcr.io/sanger-tol/longranger:2.2.2-c3"
        }
    }

    withName: BCFTOOLS_CONSENSUS {
        // Filter by mapping quality, dont keep alternative homozygotes,
        // keep longer allele
        ext.args = '-i\'QUAL>1 && (GT="AA" || GT="Aa")\' -Hla'
    }

    withName: BCFTOOLS_INDEX {
        ext.args = '--tbi'
    }

    withName: BCFTOOLS_VIEW {
        // Dont keep command line information, keep reference allele or
        // suggested alternative if reference is N
        ext.args = '--no-version -e\'type="ref"||REF~"N"\''
    }

    withName: BCFTOOLS_NORM {
        ext.args = '--no-version'
    }

    withName: SEQTK_SUBSEQ_PRIMARY {
        ext.prefix = '.primary'
    }

    withName: SEQTK_SUBSEQ_HAPLOTIGS {
        ext.prefix = '.haplotig'
    }

    // End of Set up of the polishing pipeline

    // Set up of the Hi-C read mapping pipeline
    withName: BWAMEM2_MEM {
        // Set read group in the output file header as specified in meta;
        // If the data is HiC then set for split alignment, take the alignment
        // with the smallest coordinate aas primary, skip mate rescue,
        // skip pairing, perform smart pairing,
        // and append FASTQ comment to SAM output
        ext.args = { ( "${meta.datatype}" == "hic" ) ? "-5SPCp -R ${meta.read_group}" : "-R ${meta.read_group}" }
    }

    withName: SAMTOOLS_FASTQ {
        // Filter out reaads which fail platform/vendor quality checks;
        // dont append /1 and /2 to the read name;
        // copy RG, BC and QT tags to the FASTQ header line
        ext.args = '-F 0x200 -nt'
    }

    withName: SAMTOOLS_SORT {
        ext.prefix = { "${meta.id}.sorted" }
    }

    withName: SAMTOOLS_COLLATE {
        ext.prefix = { "${meta.id}.collated" }
    }

    withName: SAMTOOLS_FIXMATE {
        // Add mate score tag
        ext.args = '-m'
        ext.prefix = { "${meta.id}.fixmate" }
    }

    withName: SAMTOOLS_MARKDUP {
        ext.prefix = { "${meta.id}.markdup" }
    }

    withName: SAMTOOLS_VIEW {
        ext.args = "--output-fmt cram"
    }

    withName: SAMTOOLS_VIEW_MARKDUP {
        // Filter out reads classified as PCR or optical duplicates;
        // Return compressed BAM output
        ext.args = "-u -F0x400 --output-fmt bam"
    }

    withName: GNU_SORT {
        ext.args = '-k 1,1 -k2,2n'
    }
    // End of Set up of the Hi-C read mapping pipeline

    // Set up of the scffolding pipeline
    withName: 'YAHS' {
        // Skip the initial assembly error correction step
        ext.args = '--no-contig-ec'
    }

    withName: 'COOLER_CLOAD' {
        // Positions in the input file are zero-based;
        // chrom1 field number (one-based) is 2;
        // pos1 field number (one-based) is 3;
        // chrom2 field number (one-based) is 6;
        // pos2 field number (one-based) is 7
        ext.args = 'pairs -0 -c1 2 -p1 3 -c2 6 -p2 7'
    }

    withName: 'PRETEXTSNAPSHOT' {
        // Make one plot containing all sequences
        ext.args = '--sequences \"=full\"'
    }

    withName: 'JUICER_TOOLS_PRE' {
        ext.juicer_tools_jar = 'juicer_tools.1.9.9_jcuda.0.8.jar'
        ext.juicer_jvm_params = '-Xms6g -Xmx48g'
    }

    withName: 'JUICER_PRE' {
        ext.args2 = "LC_ALL=C sort -k2,2d -k6,6d -S50G | awk '\$3>=0 && \$7>=0'"
    }
    // End of Set up of the scaffolding  pipeline

    //Set up of assmebly stats subworkflow
    withName: 'BUSCO' {
        ext.args = "--mode genome"
    }
    //End of Set up of assmebly stats subworkflow


    //Set up purge_dups subworkflow
    withName: 'MINIMAP2_ALIGN_READS' {
        //these options from pbmm2 CSS preset
        ext.args = "-k19 -w10 -O5,56 -E4,1 -A2 -B5 -z400,50 -r2000 --lj-min-ratio 0.5"
    }

    withName: 'MINIMAP2_ALIGN_ASSEMBLY' {
        ext.args   = "-xasm5 -DP"
    }

     withName: 'PURGEDUPS_PURGEDUPS' {
        ext.args   = "-2"
    }
    //End of Set up purge_dups subworkflow
}
